buildscript {
    repositories {
        jcenter()
    }

    dependencies {
        classpath 'com.github.jengelman.gradle.plugins:shadow:1.2.2'
    }
}

plugins {
    id 'org.ajoberstar.github-pages' version '1.3.2'
}

def versions = [
        sphereSdkJvm: '1.0.0-M24',
        hamcrest: '1.3'
]

allprojects {
    group = 'com.commercetools'
    version = '0.1-SNAPSHOT'

    project.buildDir = 'target'
}

subprojects {

    apply plugin: 'java'

    sourceCompatibility = '1.8'

    repositories {
        mavenCentral()
    }

    dependencies {
        compile 'com.google.guava:guava:18.0'
        compile "io.sphere.sdk.jvm:sphere-java-client:$versions.sphereSdkJvm"
        compile "io.sphere.sdk.jvm:sphere-models:$versions.sphereSdkJvm"
        compile 'org.javamoney:moneta:1.0'
        compile 'org.slf4j:slf4j-log4j12:1.7.13'

        testCompile 'junit:junit:4.12'
        testCompile "org.hamcrest:hamcrest-core:$versions.hamcrest"
        testCompile "org.hamcrest:hamcrest-library:$versions.hamcrest"
    }

}

task wrapper(type: Wrapper) {
    gradleVersion = '2.9'
}

project(":service") {
    apply plugin: 'com.github.johnrengelman.shadow'

    dependencies {
        compile 'com.mashape.unirest:unirest-java:1.4.7'
        compile('com.sparkjava:spark-core:2.3') {
            exclude group: 'org.slf4j', module: 'slf4j-simple'
        }
        compile 'org.apache.httpcomponents:fluent-hc:4.5.1'
        compile 'org.quartz-scheduler:quartz:2.2.2'

        testCompile 'org.assertj:assertj-core:3.2.0'
        testCompile 'org.exparity:hamcrest-date:2.0.1'
        testCompile 'org.mockito:mockito-core:1.10.19'
    }
}

/**
 * Asserts that the environment variables required for running the executable specification are set.
 *
 * @param project the project
 * @param test the test task to assert the variables for
 */
void assertPropertiesAreDefined(Project project, Test test) {
    test.systemProperty 'concordion.output.dir', project.reporting.baseDir.getAbsolutePath() + "/spec"
    test.systemProperty 'concordion.extensions', 'org.concordion.ext.inputstyle.InputStyleExtension,org.concordion.ext.runtotals.RunTotalsExtension'

    test.systemProperty 'CT_PROJECT_KEY', project.hasProperty('CT_PROJECT_KEY') ? project.CT_PROJECT_KEY : System.getenv('CT_PROJECT_KEY')
    assertPropertyIsNotNullOrEmpty('CT_PROJECT_KEY', test.getSystemProperties())

    test.systemProperty 'CT_CLIENT_ID', project.hasProperty('CT_CLIENT_ID') ? project.CT_CLIENT_ID : System.getenv('CT_CLIENT_ID')
    assertPropertyIsNotNullOrEmpty('CT_CLIENT_ID', test.getSystemProperties())

    test.systemProperty 'CT_CLIENT_SECRET', project.hasProperty('CT_CLIENT_SECRET') ? project.CT_CLIENT_SECRET : System.getenv('CT_CLIENT_SECRET')
    assertPropertyIsNotNullOrEmpty('CT_CLIENT_SECRET', test.getSystemProperties())

    test.systemProperty 'CT_PAYONE_INTEGRATION_URL', project.hasProperty('CT_PAYONE_INTEGRATION_URL') ? project.CT_PAYONE_INTEGRATION_URL : System.getenv('CT_PAYONE_INTEGRATION_URL')
    assertPropertyIsNotNullOrEmpty('CT_PAYONE_INTEGRATION_URL', test.getSystemProperties())

    test.systemProperty 'TEST_DATA_VISA_CREDIT_CARD_NO_3DS', project.hasProperty('TEST_DATA_VISA_CREDIT_CARD_NO_3DS') ? project.TEST_DATA_VISA_CREDIT_CARD_NO_3DS : System.getenv('TEST_DATA_VISA_CREDIT_CARD_NO_3DS')
    assertPropertyIsNotNullOrEmpty('TEST_DATA_VISA_CREDIT_CARD_NO_3DS', test.getSystemProperties())

    test.systemProperty 'TEST_DATA_VISA_CREDIT_CARD_3DS', project.hasProperty('TEST_DATA_VISA_CREDIT_CARD_3DS') ? project.TEST_DATA_VISA_CREDIT_CARD_3DS : System.getenv('TEST_DATA_VISA_CREDIT_CARD_3DS')
    assertPropertyIsNotNullOrEmpty('TEST_DATA_VISA_CREDIT_CARD_3DS', test.getSystemProperties())
}

project(":functionaltests") {
    dependencies {
        compile project(":service")

        testCompile 'org.concordion:concordion:1.5.1'
        testCompile 'org.concordion:concordion-input-style-extension:0.1'
        testCompile 'org.concordion:concordion-run-totals-extension:1.0.0'

    }

    compileJava.dependsOn ':service:jar'

    test {
        description 'This task is supposed to run single tests, only.'
    }

    test.doFirst {
        assertPropertiesAreDefined(project, test)
    }

    task testSpec(type: Test) {
        description 'This task tests the executable Concordion specification.'

        filter {
            includeTestsMatching 'specs.SpecsFixture'
        }

        if (project.hasProperty('publish.anyway')) {
            // do not fail the build in case of failing tests, so that the reports can be published
            ignoreFailures = true
        }
    }

    testSpec.doFirst {
        assertPropertiesAreDefined(project, testSpec)
    }
}

def staticPages = project.copySpec {
    from 'ghpages'
}

def generatedPages = project.copySpec {
    into 'latest/spec'
    from 'functionaltests/target/reports/spec'
}

githubPages {
    repoUri = 'git@github.com:commercetools/commercetools-payone-integration.git'

    credentials {
        username = System.getenv('GH_TOKEN')
        password = ''
    }

    pages {
        into '.'
        with staticPages, generatedPages
    }
}

prepareGhPages.dependsOn ':functionaltests:testSpec'

void assertPropertyIsNotNullOrEmpty(String name, Map<String, Object> properties) {
    def property = properties.get(name)
    if ((property == null) || property.isEmpty()) {
        throw new GradleException('Property or environment variable ' + name + ' must not be null or empty.')
    }
}

task stage {
    description 'This task is run by Heroku and generates the service jar including all dependencies.'
    dependsOn ':service:shadowJar'
}

task ciBuild {
    description 'This task is run by Travis. It builds the service and runs its unit tests. In addition it builds the functionaltests.'
    dependsOn ':service:test', ':functionaltests:compileTestJava'
}
